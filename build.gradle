plugins {
    id "java"
    id "checkstyle"
    id "jacoco"
    id "maven"
    id 'maven-publish'
    id "signing"
    id "org.embulk.embulk-plugins" version "0.5.5"
}

repositories {
    mavenCentral()
}

def embulkVersion = '0.10.31'

group = "com.treasuredata.embulk.plugins"
version = "0.4.3-SNAPSHOT"
description = "Loads records From Zendesk"

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
    options.encoding = "UTF-8"
}

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    compileOnly "org.embulk:embulk-api:$embulkVersion"
    compileOnly "org.embulk:embulk-spi:$embulkVersion"

    compile("org.embulk:embulk-util-config:0.3.1") {
        // Conflict with Embulk Core
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-annotations'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-jdk8'
        exclude group: 'javax.validation', module: 'validation-api'
    }
    compile 'org.embulk:embulk-util-retryhelper:0.8.2'
    compile "org.embulk:embulk-util-json:0.1.1"
    compile "org.embulk:embulk-util-guess:0.2.0"

    compile 'javax.validation:validation-api:1.1.0.Final'
    compile "com.fasterxml.jackson.core:jackson-annotations:2.6.7"
    compile "com.fasterxml.jackson.core:jackson-core:2.6.7"
    compile "com.fasterxml.jackson.core:jackson-databind:2.6.7"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-guava:2.6.7"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.6.7"
    compile "commons-io:commons-io:2.7"
    compile "org.json:json:20200518"

    compile 'com.google.guava:guava:18.0'
    compile("org.apache.httpcomponents:httpclient:4.5.6") {
        exclude group: 'commons-logging', module: 'commons-logging'
    }

    // Instead of "commons-logging:commons-logging:1.2" that is required from Apache httpclient,
    // we use "jcl-over-slf4j" because Embulk is based on SLF4J.
    compile("org.slf4j:jcl-over-slf4j:1.7.12") {
        // slf4j-api is in embulk-core / embulk-api.
        exclude group: "org.slf4j", module: "slf4j-api"
    }

    testCompile "junit:junit:4.+"
    testCompile "org.embulk:embulk-core:$embulkVersion:tests"
    testCompile "org.embulk:embulk-deps:$embulkVersion"
    testCompile "org.embulk:embulk-junit4:$embulkVersion"
    testCompile "org.mockito:mockito-core:2.+"
}

embulkPlugin {
    mainClass = "org.embulk.input.zendesk.ZendeskInputPlugin"
    category = "input"
    type = "zendesk"
}

javadoc {
    options {
        locale = "en_US"
        encoding = "UTF-8"
    }
}

jar {
    from rootProject.file("LICENSE")
}

sourcesJar {
    from rootProject.file("LICENSE")
}

javadocJar {
    from rootProject.file("LICENSE")
}

// It should not publish a `.module` file in Maven Central.
// https://docs.gradle.org/current/userguide/publishing_gradle_module_metadata.html#sub:disabling-gmm-publication
tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.name

            from components.java  // Must be "components.java". The dependency modification works only for it.
            // javadocJar and sourcesJar are added by java.withJavadocJar() and java.withSourcesJar() above.
            // See: https://docs.gradle.org/current/javadoc/org/gradle/api/plugins/JavaPluginExtension.html

            pom {  // https://central.sonatype.org/pages/requirements.html
                packaging "jar"

                name = project.name
                description = project.description
                url = "https://github.com/treasure-data/embulk-input-zendesk"

                licenses {
                    license {
                        // http://central.sonatype.org/pages/requirements.html#license-information
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }

                developers {
                    // Authors after reimplementing it in Java.
                    developer {
                        name = "Hieu Duong"
                        email = "duongminhhieu89@gmail.com"
                    }
                    developer {
                        name = "Ngoc Hoang"
                        email = "ngoc.hc91@gmail.com"
                    }
                }

                scm {
                    connection = "scm:git:git://github.com/treasure-data/embulk-input-zendesk.git"
                    developerConnection = "scm:git:git@github.com:treasure-data/embulk-input-zendesk.git"
                    url = "https://github.com/treasure-data/embulk-input-zendesk"
                }
            }
        }
    }

    repositories {
        maven {  // publishMavenPublicationToMavenCentralRepository
            name = "mavenCentral"
            if (project.version.endsWith("-SNAPSHOT")) {
                url "https://oss.sonatype.org/content/repositories/snapshots"
            } else {
                url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            }

            credentials {
                username = project.hasProperty("ossrhUsername") ? ossrhUsername : ""
                password = project.hasProperty("ossrhPassword") ? ossrhPassword : ""
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}

jacocoTestReport {
    group = "Reporting"
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/reports/coverage")
    }
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}

checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = false
}
tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
    options.encoding = "UTF-8"
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = false
}

task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}

gemPush {
    host = "https://rubygems.org"
}

gem {
    authors = [ "hieu.duong" ]
    email = [ "duongminhhieu89@gmail.com" ]
    summary = "Zendesk input plugin for Embulk"
    homepage = "https://github.com/treasure-data/embulk-input-zendesk"
    licenses = [ "Apache-2.0" ]

    from rootProject.file("LICENSE")
}

clean { delete "${project.name}.gemspec" }

test {
    testLogging {
        events "passed", "skipped", "failed"
    }
}